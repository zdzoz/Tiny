main
var px, py, mval;

function mandelbrot(x,y);
var iters, x2, go,x0,y0; 
{
    let x0 <- x;
    let y0 <- y;
    let iters <- 0;
    let go <- 1;
    while go != 0 do
        if x*x+y*y > 4*10000*10000 then
            let go <- 0;
        fi;
        if iters >= 100 then
            let go <- 0;
        fi;
        if go != 0 then
            let x2 <- (x*x-y*y)/10000 + x0;
            let y <- (2*x*y)/10000 + y0;
            let x <- x2;
            let iters <- iters+1;
        fi;
    od;

    return iters;
};


{
    let px <- 0;
    let py <- 0;
    while py < 200 do
        let px <- 0;
        while px < 200 do
            let mval <- call mandelbrot( ((px-100)*4*10000)/200, ((py-100)*4*10000)/200);
            if mval == 100 then
                call OutputNum(8);
            else
                call OutputNum(1);
            fi;
            let px <- px + 1;
        od;
        let py <- py + 1;
        call OutputNewLine();
    od;


}.